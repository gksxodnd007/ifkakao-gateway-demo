plugins {
    id 'org.springframework.boot' version '2.3.2.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'java'
}

ext {
    set('springCloudVersion', "Hoxton.SR6")
}

allprojects {
    group = 'com.kakaopay'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '1.8'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    dependencies {
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
    }

    test {
        useJUnitPlatform()
    }
}

project(':spring-cloud-gateway') {
    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

    dependencies {
        implementation 'org.springframework.cloud:spring-cloud-starter-gateway'
    }
}

project(':zuul2') {
    apply plugin: 'groovy'

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter'
        implementation 'org.apache.httpcomponents:httpclient:4.5.12'
        implementation 'com.netflix.zuul:zuul-core:2.1.5'
    }

    sourceSets {
        main {
            java { srcDirs = [] } // no source dirs for the java compiler
            groovy { srcDirs = ["src/main/java", "src/main/groovy"] }  // compile everything in src/ with groovy
            resources { srcDirs = ["src/main/resources"] }
        }
    }
}

project(':zuul') {
    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

    dependencies {
        implementation 'org.springframework.cloud:spring-cloud-starter-netflix-zuul'
    }
}